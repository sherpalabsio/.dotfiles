#!/bin/zsh

# Paths and variables
export PATH=/usr/local/bin:$PATH
export PATH=/usr/local/sbin:$PATH
export DOTFILES_PATH=$HOME/.dotfiles
export DROPBOX_PATH=$(jq -r '.personal.path' ~/.dropbox/info.json)

# Zsh settings
plugins=(git zsh-syntax-highlighting)
COMPLETION_WAITING_DOTS="true"

# Zsh history
export HISTFILE="$DROPBOX_PATH/backup/.dotfiles/.zsh_history"
export HISTSIZE=50000
export SAVEHIST=$HISTSIZE
setopt INC_APPEND_HISTORY   # Don't wait until the shell exits to append to the history file
setopt HIST_FIND_NO_DUPS    # When searching for history entries, show matching lines once
setopt HIST_IGNORE_ALL_DUPS # Remove duplicates before adding a new entry
setopt HIST_IGNORE_SPACE    # Don't save commands starting with a space
setopt HIST_IGNORE_DUPS     # Don't save the command if it was just run
setopt HIST_SAVE_NO_DUPS
setopt HIST_EXPIRE_DUPS_FIRST # Expire duplicate entries before unique ones when trimming history

# Don't save non-existent commands to history
zshaddhistory() {
  local -r line=$1
  local -r command_name=${${(z)line}[1]}

  command -v "$command_name" > /dev/null
}

# Load Oh-My-Zsh
source "$HOME/.oh-my-zsh/oh-my-zsh.sh"

# asdf config
asdf_sh_path="$(brew --prefix asdf)/libexec/asdf.sh" # (homebrew install)
[[ -s $asdf_sh_path ]] && source $asdf_sh_path

# Load all .sh files recursively
_my_files=($DOTFILES_PATH/**/*.sh)
_my_files=(${_my_files[@]/*\/lib\/*/})
_my_files=(${_my_files[@]/*\/tmp\/*/})
_my_files=(${_my_files[@]/*\/local\/*/})
_my_widgets=($DOTFILES_PATH/zsh/widgets/**/*.plugin.zsh)

_my_files=($_my_files $_my_widgets)

for file in ${_my_files}; do
  source $file
done

unset _my_widgets _my_files

source $DOTFILES_PATH/zsh/prompt.zsh

# Load super secret stuff
for file in $DOTFILES_PATH/local/**/*.sh; do
  source $file
done

include() {
  [ -f "$1" ] && source "$1"
}

include ~/.fzf.zsh

run_lazygit() { lazygit; }
zle -N run_lazygit
bindkey '^G' 'run_lazygit'

alias use_sherpa_dev_version="mkdir -p ~/.config/local_sherpa && touch ~/.config/local_sherpa/use_dev_version"
alias use_sherpa_prod_version="rm -f ~/.config/local_sherpa/use_dev_version"

if [[ -f "$HOME/.config/local_sherpa/use_dev_version" ]]; then
  export USE_SHERPA_DEV_VERSION=true
  include ~/projects/local_sherpa/lib/init.sh
else
  include $DOTFILES_PATH/lib/local_sherpa/lib/init.sh
fi

eval $(thefuck --alias)
